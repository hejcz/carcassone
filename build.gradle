plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
}

group 'io.github.hejcz'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/spekframework/spek-dev" }
}

configurations {
    ktlint
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // formatter
    ktlint "com.pinterest:ktlint:0.33.0"

    // spek
    testImplementation 'org.amshove.kluent:kluent:1.4'
    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.9'
    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.9'
    testRuntimeOnly 'org.spekframework.spek2:spek-runner-junit5:2.0.9'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

test {
    useJUnitPlatform {
        includeEngines 'spek2', 'junit-jupiter'
    }
}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}